// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: 'buildsystem/dependencies.gradle'

buildscript {
  configurations.all {
    resolutionStrategy {
      dependencySubstitution {
        substitute module('com.tunnelvisionlabs:antlr4') with module('org.antlr:antlr4:4.5.3')
      }
    }
  }
  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
    maven {
      // For fabric
      url 'https://maven.fabric.io/public'
    }
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {

    classpath 'com.android.tools.build:gradle:2.2.0-beta2'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
    classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.5.4'
    classpath 'de.mobilej.unmock:UnMockPlugin:0.5.0'
    classpath 'com.ofg:uptodate-gradle-plugin:1.6.2'
    classpath 'com.github.okbuilds:okbuild-gradle-plugin:0.3.1'
    classpath "net.rdrei.android.buildtimetracker:gradle-plugin:0.8.0"
    classpath 'com.frogermcs.androiddevmetrics:androiddevmetrics-plugin:0.4'
    classpath 'com.bugtags.library:bugtags-gradle:1.2.6'
    classpath 'com.github.promeg:android-git-sha-plugin:1.0.0'
    classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.8"
    classpath 'com.squareup.sqldelight:gradle-plugin:0.4.4'
    classpath 'com.jakewharton:butterknife-gradle-plugin:8.2.1'
    classpath 'com.github.triplet.gradle:play-publisher:1.1.5'
  }
  configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

allprojects {
  repositories {
    jcenter()
    mavenCentral()
    maven {
      // For icepick
      url 'https://clojars.org/repo/'
    }
    maven {
      // For fabric
      url 'https://maven.fabric.io/public'
    }
    maven { url "https://jitpack.io" }
    maven {
      url "http://dl.bintray.com/piasy/maven"
    }
    maven {
      url "http://dl.bintray.com/okbuild/maven"
    }
  }
}

subprojects {
  afterEvaluate {
    // Log instrumentation tests results.
    tasks.withType(com.android.build.gradle.internal.tasks.AndroidTestTask) { task ->
      task.doFirst {
        logging.level = LogLevel.INFO
      }
      task.doLast {
        logging.level = LogLevel.LIFECYCLE
      }
    }
  }
}

apply from: 'AndroidCodeQualityConfig/jacoco.gradle'
apply from: 'buildsystem/okbuck.gradle'

apply plugin: "build-time-tracker"
buildtimetracker {
  reporters {
    csv {
      output "build/times.csv"
      append true
      header false
    }

    summary {
      ordered false
      threshold 50
      barstyle "unicode"
    }

    csvSummary {
      csv "build/times.csv"
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
